# Finds the FLAC library

# This file can be configured with the following CMake variables:
# - FLAC_ROOT_DIR
# - FLAC_INCLUDE_DIR
# - FLAC_LIBRARY

# Or, alternatively, the following environment variables:
# - FLAC_INSTALL_DIR
# - LIBRARY_PATH

include(FindPackageHandleStandardArgs)
find_package(PkgConfig QUIET)

pkg_check_modules(PC_FLAC QUIET FLAC)

set(FLAC_DEFINITIONS ${PC_FLAC_CFLAGS_OTHER})
set(FLAC_SEARCH_DIR ${FLAC_ROOT_DIR} ${CONAN_FLAC_ROOT} $ENV{FLAC_INSTALL_DIR})

find_path(FLAC_INCLUDE_DIR
  FLAC/all.h
  HINTS ${FLAC_SEARCH_DIR} ${PC_FLAC_INCLUDEDIR} ${PC_FLAC_INCLUDE_DIRS}
  PATH_SUFFIXES include
)

find_library(FLAC_LIBRARY
  NAMES FLAC
  HINTS ${FLAC_SEARCH_DIR} ${PC_FLAC_LIBDIR} ${PC_FLAC_LIBRARY_DIRS}
  PATH_SUFFIXES lib bin
  ENV LIBRARY_PATH
)

set(FLAC_LIBRARIES ${FLAC_LIBRARY})
set(FLAC_INCLUDE_DIRS ${FLAC_INCLUDE_DIR})
set(FLAC_VERSION ${PC_FLAC_VERSION})

find_package_handle_standard_args(FLAC
  REQUIRED_VARS FLAC_LIBRARIES FLAC_INCLUDE_DIR
  VERSION_VAR FLAC_VERSION
  HANDLE_COMPONENTS
)

if(FLAC_FOUND)
  add_library(FLAC::FLAC UNKNOWN IMPORTED)
  set_target_properties(FLAC::FLAC PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${FLAC_INCLUDE_DIRS}"
    IMPORTED_LOCATION "${FLAC_LIBRARY}"
    INTERFACE_COMPILE_OPTIONS "${FLAC_DEFINITIONS}"
  )
endif()

mark_as_advanced(FLAC_INCLUDE_DIRS FLAC_LIBRARIES)
